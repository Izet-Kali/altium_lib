*******************************************************************
******STMicroelectronics MOSFET, IGBT and Bipolar Library *********
*******************************************************************
*							          *
* Models provided by STMicroelectronics are not guaranteed to	  *
* fully represent all the specifications and operating		  *
* characteristics of the product behavior that they reproduce.	  *
* The model describes the characteristics of a typical device.	  *
* In all cases, the current product data sheet contains all	  *
* information to be used like final design guidelines and the	  *
* only actual performance specification.			  *
* Altough models can be a useful tool in evaluating device	  *
* performance, they cannot model exact device performance under	  *
* all conditions.						  *
* STMicroelectronics therefore does not assume any		  *
* responsibility arising from their use.			  *
* STMicroelectronics reserves the right to change models	  *
* without prior notice.						  *
*								  *
* Note: The model doesn't take into account the drain, gate,	  *
* source inductances.If these contributions have to be		  *
* considered it is possible include the inductors externally.	  *
* For this package the values can be estimated as follow	  *
*								  *
* Ldrain= 1nH ,Lsource=2nH and Lgate=2.5nH			  *
*							          *
* Rev 2.0 - 11 July 2013		      			  *
*******************************************************************

.SUBCKT STD100N3LF3_V2 1 2 3
*******************************************************************
E1 Tj val_T VALUE={TEMP}
R1 val_T 0 1E-03
***********************************************************
Rtk Tj 0 1E10
Rtk1 Tj 0 1E10
************************************
VLd 1 d1k 0
VLs ss 3 0
VLG 2 g2 0
rg1 g2 g {rg}
*Rdsx s dx 100k
*Cdx dx d 50p
*******************************************************************
.PARAM Area=1 BVDSS=1 raval=10m
***************************************************
.PARAM  rg=1.9 Vth0=3.4
.PARAM  lambda=0.025 
.PARAM  kpsat0=110 kplin0=135 drs=0.1m

.PARAM  rd=3.5m 
.PARAM  rpa=1E-06

***********************************
.PARAM  unt =-1.6 vthx=4.5m ksat=-1 klin= -1
.PARAM  a=1 b=1 Rx=1.2


E_soglia soglia 0 value {vth1(V(TJ))}
R_soglia soglia 0 1


***********************************************************
.FUNC r_s(T) {((T+273)/300)**(unt)/drs}
.FUNC vth1(x) {vth0-vthx*(x-27)}
.FUNC kpsat(x) {kpsat0*((x+273)/300)**(ksat)}
.FUNC kplin(x) {kplin0*((x+273)/300)**(klin)}
.FUNC un(T) {b*((T+273)/300)**(unt)}
R_lambda L 0 1
V_lambda L 0 {lambda}
***********************************************************
***********************************************************
Gmos d s value {Area*((IF((limit((v(g,s)),-15,15))<v(soglia),0,IF(abs(v(d,s))<((limit((v(g,s)),-15,15))-v(soglia))*
+kpsat(V(TJ))/kplin(V(TJ)),
+(1+V(L)*abs(v(d,s)))*sgn(V(d,s))*(kplin(V(TJ))/2*abs(v(d,s))*(2*((limit((v(g,s)),-15,15))-v(soglia))-
+kplin(V(TJ))/kpsat(V(TJ))*abs(v(d,s)))),
+(1+v(L)*abs(v(d,s)))*sgn(V(d,s))*(kpsat(V(TJ))/2*((limit((v(g,s)),-15,15))-v(soglia))**2)*1))))}


Gmos2 d s value {(100u*Area)*((IF((limit((v(g,s)),-15,15))<(v(soglia)*0.28),0,IF(abs(v(d,s))<((limit((v(g,s)),-15,15))
+-(v(soglia)*0.28))*kpsat(V(TJ))/kplin(V(TJ)),
+(1+V(L)*abs(v(d,s)))*sgn(V(d,s))*(kplin(V(TJ))/2*abs(v(d,s))*(2*((limit((v(g,s)),-15,15))-(v(soglia)*0.28))-
+kplin(V(TJ))/kpsat(V(TJ))*abs(v(d,s)))),
+(1+v(L)*abs(v(d,s)))*sgn(V(d,s))*(kpsat(V(TJ))/2*((limit((v(g,s)),-15,15))-(v(soglia)*0.28))**2)*1))))}

 
Gmos3 d s value {(0.05*Area)*((IF((limit((v(g,s)),-15,15))<(v(soglia)*0.91),0,IF(abs(v(d,s))<((limit((v(g,s)),-15,15))-
+(v(soglia)*0.91))*kpsat(V(TJ))/kplin(V(TJ)),
+(1+V(L)*abs(v(d,s)))*sgn(V(d,s))*(kplin(V(TJ))/2*abs(v(d,s))*(2*((limit((v(g,s)),-15,15))-(v(soglia)*0.91))-
+kplin(V(TJ))/kpsat(V(TJ))*abs(v(d,s)))),
+(1+v(L)*abs(v(d,s)))*sgn(V(d,s))*(kpsat(V(TJ))/2*((limit((v(g,s)),-15,15))-(v(soglia)*0.91))**2)*1))))}

G_RMos d1k dd VALUE={Area*((un(V(TJ))*(V(d1k,d)))/(rd+rpa*((I(V_sense)**a))**Rx))}
G_rs s ss VALUE={(Area*(r_s(V(TJ))*v(s,ss))/drs)}
R_Grs s ss 1E03
R_Rmos d1k dd 1E03
V_sense dd d 0
***********************************************************
rdd dd 0 1E10
rdd1 d 0 1E10
rdd3 s 0 1E10
R_DS d s 1E10
***********************************************************
CGS g 3  {2081p*Area}
R_CGS g 3 500E06
***********************************************************
Edev dev1 0 value {v(d,g)}
c_dev1 dev1 dev2 1p
R_dev dev2 dev3 1u
V_miller dev3 0 0 
G_miller d g  value ={v(alfa)*i(V_miller)*1E12}
**************************  C(V) **************************
Rcap alfa 0 1k
Ecap alfa 0  TABLE ={V(d,s)}
+(0,1727p) 
+(0.25,1681p) 
+(0.5,1486p) 
+(1,1392p) 
+(2,854p) 
+(3,686p) 
+(5,277p) 
+(7,124.4p) 
+(10,109.6p) 
+(15,92p) 
+(25,66p) 
+(26,65.9p) 
+(30,65.2p) 

Edevc dev1c 0 value {v(d,s)}
c_dev1c dev1c dev2c 1p
R_devc dev2c dev3c 1
V_millerc dev3c 0 0 
G_coss d s  value ={v(alfa2)*i(V_millerc)*1E12}
Ecap2 alfa2 0  TABLE ={V(d,s)}
+(0,5180p) 
+(0.25,4480p) 
+(0.5,3900p) 
+(1,3120p) 
+(2,2407p) 
+(3,1970p) 
+(5,1560p) 
+(7,1311p) 
+(10,1094p) 
+(15,920p) 
+(25,735p) 
+(26,725p) 
+(30,705p) 

***********************************************************
***********************************************************
R_bvdss	d1k d1bvdss1 {raval}
V_bvdss	d1bvdss1 d1bvdss2 0
G_bvd	d1bvdss2 ss VALUE={I_BVDSS(V(d1bvdss2,ss),v(Tj))}
R_GBDSS	d1bvdss2 0 1E10
.FUNC I_BVDSS(z,k1) {(exp(min(-175+z/(bvd(k1)),7))-10E-12)}
***********************************************************
.FUNC bvd(k) {0.19*BVDSS+0.0002*k}
***********************************************************
.FUNC Rdiodo(rdid) {rdid0*((rdid+273)/300)**(rdid_temp)}
.PARAM rdid0={1.25/Area} rdid_temp=1
G_R_didd d1k d1z VALUE={200*(V(d1k,d1z)/Rdiodo(V(TJ)))}

R_G_R_didd d1k d1z {1/Area}

V_diodo d1zd d1z 0

G_diode 3 d1zd VALUE={I_diode(V(3,d1zd),v(Tj))}
R_x 3 d1zd 1E9

.FUNC I_diode(z3,k3) {(exp(min(-15+z3/(did(k3)),7))-100n)}


.FUNC did(k4) {0.06-85E-06*k4}

***********************************************************
R_R003 aa 0 500E06
R_edep d1k d_dedep 150
E_Eds d_dedep edep VALUE {-V(aa,0)*1k}
C_Cds edep ss {18E-12*Area}
E_E001 ba 0 VALUE {-I(V_diodo)*(0.0016*v(Tj)+1)}
R_R001 aa ba 25
C aa 0 22p
***********************************************************

.ENDS STD100N3LF3_V2

* END OF MODELLING
