* AD8216 SPICE Macro-model        
* Description: Single Supply Difference Amplifier
* Generic Desc: 5V typical supply, 12.5V Max, XF65
* Developed by: DK
* Revision History: 
* 1.0 (9/2012) � DK � initial release
* Copyright 2012 by Analog Devices, Inc.
*
* Refer to http://www.analog.com/Analog_Root/static/techSupport/designTools/spiceModels/license/spice_general.html 
* for License Statement. Use of this model indicates your acceptance
* of the terms and provisions in the License Statement.
* 
* BEGIN Notes:
*      
* Not Modeled:
*�Temperature effects
* 
* Parameters modeled include:  
* Closed loop gain and phase vs bandwidth
* Output voltage limiting
* Offset voltage vos = (vout/gain)-vin
* Slew rate
* Voltage spectral noise
* PSRR and CMRR
* Input common mode voltage range
* Cap load response
* END Notes
*
* Node Assignments
******************************************************
*                 noninverting input
*                 |    inverting input
*                 |    |
*                 |    |    Vref1
*                 |    |    |
*                 |    |    |    Vref2
*                 |    |    |    |     Vcc
*                 |    |    |    |     |
*		      |    |    |    |     |    Out
*                 |    |    |    |     |    |    ground
*                 |    |    |    |     |    |    |
.SUBCKT AD8216   +IN -IN  VREF1 VREF2 VCC  OUT  GND   NC


*** Resistor Attenuator ***
RA1 +IN 1 200k
RB1 1 3 10.012k
RC1 3 15 909.25
RA2 -IN 2 200k
RB2 2 4 10.012k
RC2 4 15 909.25
VBias 15 GND .25
*Gain Trim resistor*
R3 4 3 46k

*** Offset resistors ***
R4 VREF1 11 50k
R5 VREF2 11 50k
R6 3 11 22.4k

*** Differential Pair ***
R7 VCC 5 253.4
R8 VCC 6 253.4
R9 8 12 150
R10 7 12 150
Q1 5 71 7 0 NPN
Q2 6 80 8 0 NPN
I1 12 GND 5e-4
CINN 80 GND 500f
CINP 71 GND 500f
EOS 71 70 poly(2) (81,100) (201,GND) 0 1 1

*** Supply Current ***
I2 VCC GND .476e-3
GCC VCC GND 14 OUT .37

*** CMRR ***

*Zero at 10kHz
GCMR 100 81 90 100 6.16e-12
RCMR 81 82 1e6
LCMR 82 100 15.915
**Zero at 5.167MHz**
*GCM2 100 83 81 100 1e-6
*RCM2 83 84 1e6
*LCM2 84 100 30.8e-3
*Zero at 19.4MHz**
*GCM3 100 85 83 100 1e-6
*RCM3 85 86 1e6
*LCM3 86 100 8.2e-3

*** 1st Gain Stage and Slew Rate ***
G1 100 9 6 5 .001
R1 9 100 500e3
DZ1 9 21 DZENER
DZ2 100 21 DZENER

*** 2nd Gain Stage ***
G2 100 10 9 100 89.3e-3
R2 10 100 1e6
C1 10 100 196.6e-9

*** High Freq Poles ***
G3 100 20 10 100 .01
R15 20 100 100
C4 20 100 123.776e-12
G4 100 30 20 100 .01
R16 30 100 100
C5 30 100 123.776e-12

*** Feedback Resistor for Closed Loop Gain of 34dB ***
R13 14 4 47.4k

*** Diff Loop Gain ***
E2 70 80 1 2 2.5

*** Spectral Noise ***
VN GND 190 .65
DN 190 200 DNOISE
RN 200 GND 1
VM GND 200 0
FN GND 201 VM 1
RZ 201 GND 1

*** Output Voltage Limit ***
D1 10 17 D
D2 16 10 D
V3 35 17 .978
EI 35 GND  VCC 0 1
V4 16 GND .84

*** Internal Reference ***
R11 VCC 13 100k
R12 13 GND 100k
E1 100 GND 13 0 1

*** Output Stage with Common Mode Voltage Range ***
EAVG 90 GND Value={ ((V(+IN) + V(-IN))*0.5 )}
EO1 95 100 30 100 1
EO2 14 GND Value={ IF( V(90) > 65, V(VCC) -.1, IF( V(90) < -4.0, V(VCC) - .1, V(95) )) }
R14 OUT 14 2


.MODEL D D
.MODEL DILIM D(IS=1e-15)
.MODEL DZENER D(BV=35.0235V, IS=1E-14, IBV=1E-3)
.MODEL NPN NPN(NF=0.391 BF=3162 IS=8E-14 CJE=0PF)
.MODEL DNOISE D(AF=0, KF=.2e-10)
.ENDS










