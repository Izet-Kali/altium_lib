* ADG5421F SPICE Macro-model
* Generic Desc: Fault Protection and Detection,
* Developed by: MPorley / ADGT
* Revision History: 1.0 (12/2020)
* Copyright 2020 by Analog Devices, Inc.
*
* Refer to http://www.analog.com/Analog_Root/static/techSupport/designTools/spiceModels/license/spice_general.html for License Statement. Use of this model
* indicates your acceptance of the terms and provisions in the License Statement.
*
* Begin Notes:
* The model will work on Vdd/Vss from 8V to 44V single supply and +/-5V to +/-22V dual supply.
* The model provides parametric specifications at +/-22V only and is not variable with Vdd and Vss changes. Please see datasheet page 3.
* The model is operational at RESET/VL level from V and 15V only.
* Ensure 120us from the time of power-up or RESET before any logic command is issued.
*
* Not Modeled:
* IDD during fault conditions.
* Fault leakage.
* Negative Overvoltage Response Time
*
* Please see Connections Section below.
*
* Parameters modeled include:
*	On Resistance
*	Ton and Toff
*	Break-Before-Make
*	Off Isolation
*	Crosstalk
*	Supply Currents: Iss/Idd
*	Bandwidth
*	Charge Injection
*	Positive Overvoltage Response Time
*	Interrupt Flag Response/Recovery Time
*
* Connections
*      1  = S1
*      2  = S2
*      3  = FF
*      4  = GND
*      5  = VDD
*      6  = VSS
*      7  = IN2
*      8  = IN1
*      9  = D2
*      10  = D1
*
.SUBCKT ADG5421F 1 2 3 4 5 6 7 8 9 10

.MODEL VEN VSWITCH(Von=5 Voff=0.8 Ron=6500000 Roff=90000)
.MODEL DCLAMP D(IS=1E-15 IBV=1E-13)
.MODEL VFF VSWITCH(Von=5 Voff=0.8 Ron=195000 Roff=5500)
.MODEL VFAULT VSWITCH(Von=5 Voff=0.8 Ron=12900000 Roff=300000)
.MODEL VON VSWITCH(Von=5 Voff=0.8 Ron=0.001 Roff=1000000000)
.MODEL VFAULT VSWITCH(Von=5 Voff=0.8 Ron=12900000 Roff=30000)
.MODEL VOVPLEAK VSWITCH(Von=5 Voff=0.1 Ron=4000000 Roff=1000000000)
.MODEL VOVNLEAK VSWITCH(Von=5 Voff=0.1 Ron=4000000 Roff=1000000000)

* CROSSTALK
C12a 1 2 0.33e-012

* IDD/ISS
I1 5 4 130e-006
I2 6 4 75e-006

*FAULT FLAG
EVFF12 993 4 VALUE = { IF((V(1)<=V(5) & V(1)>=V(6) & V(2)<=V(5) & V(2)>=V(6)), 5, 0.1) }
CF10 3 4 10e-012
SVFF11 3 4 993 4 VFF

* POS/NEG FAULT PROTECTION
SOVC1 993 1084 993 4 VFAULT
COVP1 1084 4 1e-012
SOVP10 1 44 1084 4 VON
SOVP11 2 55 1084 4 VON

* Configuration: SPST 2:2

** SWITCH 1 **
*
*FAULT SOURCE LEAKAGE
EVLEAK1 994 4 VALUE = { IF((V(1)>=V(5)), 5, 0.1) }
SOVLEAK1 1 4 994 4 VOVPLEAK
EVLEAK2 995 4 VALUE = { IF((V(1)<=V(6)), 5, 0.1) }
SOVLEAK2 1 4 995 4 VOVNLEAK
*
* OFF ISOLATION
C11 1 10 0.17e-012
*
* CHARGE INJECTION
C12 10 140 0.01e-012
C13 1 140 0.01e-012
*
* CD/CS OFF AND BANDWIDTH
C14 1 4 1e-012
C15 10 4 1e-012
*
* ON RESISTANCE
Ech155 155 4 VALUE = { IF ((ABS(V(1)))>(ABS(V(184))),V(1),V(10)) }
R155 155 4 1G
Ron11 137 10 0.5
Ech111 136 4 VALUE = { IF (V(155)<10,9.75,(0.5*(V(155)-10)*(V(155)-10))+9.75) }
RIN1	136 4	1G
EOUT1 137 181	POLY(2) (136,4) (180,4) 0 0 0 0 0.999/1000
FCOPY1	4 180 VSENSE1 1
RSENSOR1 180 4	1K
VSENSE1 181 184	0
*
* TON/ TOFF/ BBM
S11 182 184 140 4 VON
S12 143 138 143 4 VEN
Ech14 143 4 VALUE = { IF(V(8)>=1.3, 5 , 0.8 ) }
eV1 140 4 138 4 1
C16 138 4 2e-012
*
* VOLTAGE SUPPLY REQUIREMENT
S13 44 182 185 4 VON
S14 142 185 142 4 VON
Ech15 142 4 VALUE = { IF((V(6)<=-0.5 & V(6)>=-22) & (V(5)<=22 & V(5)>=5), 5 , 0.01 ) }
S15 139 185 139 4 VON
Ech16 139 4 VALUE = { IF((V(6)>=-0.5 & (V(5)<=44 & V(5)>=8)), 5 , 0.01 ) }

** SWITCH 2 **
*
*FAULT SOURCE LEAKAGE
EVLEAK3 996 4 VALUE = { IF((V(2)>=V(5)), 5, 0.1) }
SOVLEAK3 2 4 996 4 VOVPLEAK
EVLEAK4 997 4 VALUE = { IF((V(2)<=V(6)), 5, 0.1) }
SOVLEAK4 2 4 997 4 VOVNLEAK
*
* OFF ISOLATION
C21 2 9 0.17e-012
*
* CHARGE INJECTION
C22 9 240 0.01e-012
C23 2 240 0.01e-012
*
* CD/CS OFF AND BANDWIDTH
C24 2 4 1e-012
C25 9 4 1e-012
*
* ON RESISTANCE
Ech255 2555 4 VALUE = { IF ((ABS(V(2)))>(ABS(V(284))),V(2),V(9)) }
R255 2555 4 1G
R21 237 9 0.5
Ech221 241 4 VALUE = { IF (V(2555)<10,9.75,(0.5*(V(2555)-10)*(V(2555)-10))+9.75) }
RIN2	241 4	1G
EOUT2 237 281	POLY(2) (241,4) (280,4) 0 0 0 0 0.999/1000
FCOPY2	4 280 VSENSE2 1
RSENSOR2 280 4	1K
VSENSE2 281 284	0
*
* TON/ TOFF/ BBM
S21 282 284 240 4 VON
S22 243 238 243 4 VEN
Ech24 243 4 VALUE = { IF(V(7)>=1.3, 5 , 0.8 ) }
eV2 240 4 238 4 1
C26 238 4 2e-012
*
* VOLTAGE SUPPLY REQUIREMENT
S23 55 282 285 4 VON
S24 242 285 242 4 VON
Ech25 242 4 VALUE = { IF((V(6)<=-0.5 & V(6)>=-22) & (V(5)<=22 & V(5)>=5), 5 , 0.01 ) }
S25 239 285 239 4 VON
Ech26 239 4 VALUE = { IF((V(6)>=-0.5 & (V(5)<=44 & V(5)>=8)), 5 , 0.01 ) }

.ENDS ADG5421F
