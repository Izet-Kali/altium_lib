* ADG1201 SPICE Macro-model
* Generic Desc: Low Capacitance, Low Charge Injection,±15 V/+12 V, iCMOS, SPST
* Developed by: MPorley / ADGT
* Revision History: 1.0 (07/2018)
* Copyright 2018 by Analog Devices, Inc.
*
* Refer to http://www.analog.com/Analog_Root/static/techSupport/designTools/spiceModels/license/spice_general.html for License Statement. Use of this model
* indicates your acceptance of the terms and provisions in the License Statement.
*
* Begin Notes:
* The model will work on Vdd/Vss from 5V to 16.5V single supply and +/-5V to +/-16.5V dual supply.
* The model provides parametric specifications at +/-16.5V only and is not variable with Vdd and Vss changes. Please see datasheet page 3.
*
* Not Modeled:
*
* Parameters modeled include:
*	On Resistance
*	Ton and Toff
*	Break-Before-Make
*	Off Isolation
*	Supply Currents: Iss/Idd
*	Bandwidth
*	Charge Injection
* Connections
*      1  = VDD
*      2  = GND
*      3  = VSS
*      4  = IN1
*      5  = S1
*      6  = D1
*
.SUBCKT ADG1201 1 2 3 4 5 6

.MODEL VON VSWITCH(Von=5 Voff=0.8 Ron=0.001 Roff=1000000000)
.MODEL VEN VSWITCH(Von=5 Voff=0.8 Ron=160000 Roff=165000)
.MODEL DCLAMP D(IS=1E-15 IBV=1E-13)


* IDD/ISS
I1 1 2 60E-006
I2 3 2 0.001E-006

* Configuration: SPST 1:1


** SWITCH 1 **
*
* ESD PROTECTION DIODES
D11 3 6 DCLAMP
D12 6 1 DCLAMP
D13 3 5 DCLAMP
D14 5 1 DCLAMP
*
* OFF ISOLATION
C11 5 6 0.3E-012
*
* CHARGE INJECTION
C12 6 140 0.25E-012
C13 5 140 0.25E-012
*
* CD/CS OFF AND BANDWIDTH
C14 5 2 2E-012
C15 6 2 3e-012
*
* ON RESISTANCE
Ech155 1555 2 VALUE = { IF ((ABS(V(5)))>(ABS(V(184))),V(5),V(6)) }
R155 1555 2 1G
R11 137 6 0.001
S111 136 141 1141 2 VON
Ech111 1141 2 VALUE = { IF (V(1555)<-9,5,0) }
Ech11 141 2 VALUE = { IF (V(1555)<-9,3.33333333333333*(V(1555)--9)+155,0) }
S112 136 146 1146 2 VON
Ech112 1146 2 VALUE = { IF ((V(1555)>=-9) & (V(1555)<=6),5,0) }
Ech12 146 2 VALUE = { IF ((V(1555)>=-9) & (V(1555)<=6),((155-112)/((-9--1.74482582471499)*(-9--1.74482582471499)))*(V(1555)--1.74482582471499)*(V(1555)--1.74482582471499) + 112,0) }
S113 136 144 1144 2 VON
Ech113 1144 2 VALUE = { IF (V(1555)>6,5,0) }
Ech13 144 2 VALUE = { IF (V(1555)>6,-4.19047619047619*(V(1555)-6)+161,0) }
RIN1	136 2	1G
EOUT1 137 181	POLY(2) (136,2) (180,2) 0 0 0 0 0.999/1000
FCOPY1	2 180 VSENSE1 1
RSENSOR1 180 2	1K
VSENSE1 181 184	0
*
* TON/ TOFF/ BBM
S11 182 184 140 2 VON
S12 143 138 143 2 VEN
Ech14 143 2 VALUE = { IF(V(4)>=2.0, 5 , 0.8 ) }
eV1 140 2 138 2 1
C16 138 2 1e-012
*
* VOLTAGE SUPPLY REQUIREMENT
S13 5 182 185 2 VON
S14 142 185 142 2 VON
Ech15 142 2 VALUE = { IF((V(3)<=-0.5 & V(3)>=-16.5) & (V(1)<=16.5 & V(1)>=5), 5 , 0.01 ) }
S15 139 185 139 2 VON
Ech16 139 2 VALUE = { IF((V(3)>=-0.5 & (V(1)<=16.5 & V(1)>=5)), 5 , 0.01 ) }

.ENDS ADG1201
