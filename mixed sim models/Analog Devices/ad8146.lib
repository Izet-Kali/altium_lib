* AD8146 SPICE Macro-model        
* Description: Amplifier
* Generic Desc: Triple Differential Driver for HD Video
* Developed by:
* Revision History: 08/10/2012 - Updated to new header style
* 
* Copyright 2012 by Analog Devices, Inc.
*
* Refer to http://www.analog.com/Analog_Root/static/techSupport/designTools/spiceModels/license/spice_general.html for License Statement. Use of this model 
* indicates your acceptance of the terms and provisions in the License Statement.
* 
* BEGIN Notes:
*
* Not Modeled:
*    
* Parameters modeled include: 
* END Notes
*
* Node assignments
.SUBCKT AD8146  3a  9a 99 98 71  71b 110

************************** input stage ****************************************
Rf1 71 3 1k
Rf2 71b 9 1k
Rg1 3 3a 500
Rg2 9 9a 500

*****positive input left side********
I1 99 5 4E-3
Q1 98 2 5 QX
vos 3 2 -9E-3

**RAIL CLIPING****
Dlim+ 75a 14b dx
Vlim+ 99 14b 2.6
Dlim 14c 75a dx
Vlim 14c 98 2.8
Dlim- 13b 76a DX
Vlim- 13b 98 2.8
Dlim-B 76a 13C dx
Vlim-B 99 13C 2.6

** VOCM INPUT RAIL CLIPING****
DOCMa 100 100A dx
VOCMa 99 100A 3
DOCMb 100b 100 DX
VOCMb 100b 98 3

*****negative input right side*****
I2 99 6 4E-3
Q2 98 9 6 QX

***********Input capacitance/impedance*******
Cin1c 2 50 .34p
Cind 2 9 .34p
Cin2c 9 50 .34p

************************** pole, zero pole stage *****************************
****FIRST TRY**
c1 14 13 0.5p
c2 13 50 10f
c3 14 50 10f
G1 13 14  5 6  5E-3
r11 13 50 250k
r12 14 50 250k

*********pole zero stage( POSITIVE SIDE)*******
gp1 0 75 14 50 1
RP1 75 0 1
CP1 75 0 1f

*********pole zero stage( NEGATIVE SIDE)*******
gp2 0 76 13 50 1
RP2 76 0 1
CP2 76 0 1f

***************************** Output Stage ********************************
**********output stage Positive side*************
D17 76 84 DX
VO1  84 70 .177V
VO2  70 85 .177V
D16 85 76  DX
G30 70 99c 99 76  91E-3
G31 98c 70 76 98  91E-3
RO30 70 99c 11
RO31 98c 70 11
VIOUT1 99 99c 0V
VIOUT2 98 98c 0V
VIOUT3 70 71 0V

********* Output Stage Negative side ***********
D17b 75 84b DX
VO1b  84b 70b .177V
VO2b  70b 85b .177V
D16b 85b 75  DX
G30b 70b 99d 99 75  91E-3
G31b 98d 70b 75 98  91E-3
RO30b 70b 99d 11
RO31b 98d 70b 11
VIOUTB1 99 99d 0V
VIOUTB2 98d 98 0V
VIOUTB3 70b 71b 0V

*********VOCM STAGE*************************
*Cocm 100 99 3.5E-3
*R 100 99 1E3
Gocm_a 0 75 110 0 1
Gocm_b 0 76 110 0 1
Rocm1 99 100 240k
Rocm2 100 98 240k
**Voffset 100 110 -1E-3
Voffset 100 110 -10E-3

********CURRENT MIRROR TO SUPPLIES POSITVIE SIDE*********
FO1 0 99 poly(2) VIOUT1 VI1 -5.24E-3 1 -1
FO2 0 98 poly(2) VIOUT2 VI2 -5.24E-3 1 -1
FO3 0 400 VIOUT1 1
VI1 401 0 0
VI2 0 402 0
DM1 400 401 DX
DM2 402 400 DX 

********CURRENT MIRROR TO SUPPLIES NEGATIVE SIDE*********
FO1B 0 99 poly(2) VIOUTB1 VIB1 -5.24E-3 1 -1
FO2B 0 98 poly(2) VIOUTB2 VIB2 -5.24E-3 1 -1
FO3B 0 400B VIOUTB1 1
VIB1 401B 0 0
VIB2 0 402B 0
DMB1 400B 401B DX
DMB2 402B 400B DX 

Eref 50 0 Poly(2) 99 0 98 0 0 0.5 0.5
Vbf1 76 76a 0
Vbf2 75 75a 0

.MODEL QX PNP (BF=228.57 Is=1E-15)
.MODEL DX D(IS=1E-15)
.ENDS AD8146
*$


