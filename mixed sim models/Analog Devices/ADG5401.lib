* ADG5401 SPICE Macro-model
* Generic Desc: Single SPST Switch
* Developed by: MPorley / ADGT
* Revision History: 1.0 (9/2018)
* Copyright 2018 by Analog Devices, Inc.
*
* Refer to http://www.analog.com/Analog_Root/static/techSupport/designTools/spiceModels/license/spice_general.html for License Statement. Use of this model
* indicates your acceptance of the terms and provisions in the License Statement.
*
* Begin Notes:
* The model will work on Vdd/Vss from 9V to 40V single supply and +/-9V to +/-22V dual supply.
* The model provides parametric specifications at +/-22V only and is not variable with Vdd and Vss changes. Please see datasheet page 3.
*
* Not Modeled:
* Bandwidth is greater than the datasheet specs due to Sx and Dx pin capacitance variations.
*
* Parameters modeled include:
*	On Resistance
*	Ton and Toff
*	Break-Before-Make
*	Off Isolation
*	Crosstalk
*	Supply Currents: Iss/Idd
*	Bandwidth
*	Charge Injection
* Connections
*      1  = S1
*      2  = NC
*      3  = GND
*      4  = VDD
*      5  = NC
*      6  = IN1
*      7  = VSS
*      8  = D1
*
.SUBCKT ADG5401 1 2 3 4 5 6 7 8

.MODEL VON VSWITCH(Von=5 Voff=0.8 Ron=0.001 Roff=1000000000000)
.MODEL VEN VSWITCH(Von=5 Voff=0.8 Ron=1700000 Roff=3900000)
.MODEL DCLAMP D(IS=1E-15 IBV=1E-13)


* IDD/ISS
I1 4 3 45E-006
I2 7 3 0.001E-006

* Configuration: SPST 1:1


** SWITCH 1 **
*
* ESD PROTECTION DIODES
D11 7 8 DCLAMP
D12 8 4 DCLAMP
D13 7 1 DCLAMP
D14 1 4 DCLAMP
*
* OFF ISOLATION
C11 1 8 7E-012
*
* CHARGE INJECTION
C12 8 140 115E-012
C13 1 140 115E-012
*
* CD/CS OFF AND BANDWIDTH
C14 1 3 35E-012
C15 8 3 22e-012
*
* ON RESISTANCE
Ech155 1555 3 VALUE = { IF ((ABS(V(1)))>(ABS(V(184))),V(1),V(8)) }
R155 1555 3 1G
R11 137 8 0.001
S111 136 141 1141 3 VON
Ech111 1141 3 VALUE = { IF (V(1555)<-13,5,0) }
Ech11 141 3 VALUE = { IF (V(1555)<-13,2.22222222222222E-02*(V(1555)--13)+6,0) }
S112 136 146 1146 3 VON
Ech112 1146 3 VALUE = { IF ((V(1555)>=-13) & (V(1555)<=13),5,0) }
Ech12 146 3 VALUE = { IF ((V(1555)>=-13) & (V(1555)<=13),((6-4.3)/((-13-0.630511955306891)*(-13-0.630511955306891)))*(V(1555)-0.630511955306891)*(V(1555)-0.630511955306891) + 4.3,0) }
S113 136 144 1144 3 VON
Ech113 1144 3 VALUE = { IF (V(1555)>13,5,0) }
Ech13 144 3 VALUE = { IF (V(1555)>13,-1.11111111111112E-02*(V(1555)-13)+5.7,0) }
RIN1	136 3	1G
EOUT1 137 181	POLY(2) (136,3) (180,3) 0 0 0 0 0.999/1000
FCOPY1	3 180 VSENSE1 1
RSENSOR1 180 3	1K
VSENSE1 181 184	0
*
* TON/ TOFF/ BBM
S11 182 184 140 3 VON
S12 143 138 143 3 VEN
Ech14 143 3 VALUE = { IF(V(6)>=2.0, 5 , 0.8 ) }
eV1 140 3 138 3 1
C16 138 3 1e-012
*
* VOLTAGE SUPPLY REQUIREMENT
S13 1 182 185 3 VON
S14 142 185 142 3 VON
Ech15 142 3 VALUE = { IF((V(7)<=-0.5 & V(7)>=-22) & (V(4)<=22 & V(4)>=9), 5 , 0.01 ) }
S15 139 185 139 3 VON
Ech16 139 3 VALUE = { IF((V(7)>=-0.5 & (V(4)<=40 & V(4)>=9)), 5 , 0.01 ) }

.ENDS ADG5401
