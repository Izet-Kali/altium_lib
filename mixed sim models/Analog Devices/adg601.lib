* ADG601 SPICE Macro-model
* Generic Desc:
* Developed by: nespirit / ANALOG
* Revision History:  (7/2018)
* Copyright 2018 by Analog Devices, Inc.
*
* Refer to http://www.analog.com/Analog_Root/static/techSupport/designTools/spiceModels/license/spice_general.html for License Statement. Use of this model
* indicates your acceptance of the terms and provisions in the License Statement.
*
* Begin Notes:
* The model will work on Vdd/Vss from 0V to 5.5V single supply and +/-4.5V to +/-5.5V dual supply.
* The model provides parametric specifications at +/-5V only and is not variable with Vdd and Vss changes. Please see datasheet page 3.
*
* Not Modeled:
*
* Please see Connections Section below.
*
* Parameters modeled include:
* On Resistance
*	Ton and Toff
*	Break-Before-Make
*	Off Isolation
*	Crosstalk
*	Supply Currents: Iss/Idd
*  Bandwidth
*  Charge Injection
* Connections
*      1  = VDD
*      2  = S1
*      3  = VSS
*      4  = GND
*      5  = D1
*      6  = IN1
*
.SUBCKT ADG601 1 2 3 4 5 6

.MODEL VON VSWITCH(Von=5 Voff=0.8 Ron=0.001 Roff=1000000000000)
.MODEL VEN VSWITCH(Von=5 Voff=0.8 Ron=75500 Roff=95000)
.MODEL VRESET VSWITCH(Von=5 Voff=0.8 Ron=2700000 Roff=1000)
.MODEL DCLAMP D(IS=1E-15 IBV=1E-13)

* CROSSTALK

* GND 4
*R145 4 1e-015

* IDD/ISS
I1 1 4 0.001e-006
I2 3 4 0.001e-006

* Configuration: SPST 1:1


** SWITCH 1 **
*
* ESD PROTECTION DIODES
D11 3 5 DCLAMP
D12 5 1 DCLAMP
D13 3 2 DCLAMP
D14 2 1 DCLAMP
*
* OFF ISOLATION
C11 2 5 1.5e-012
*
* CHARGE INJECTION
C12 5 140 67e-012
C13 2 140 67e-012
*
* CD/CS OFF AND BANDWIDTH
C14 2 4 50e-012
C15 5 4 50e-012
*
* ON RESISTANCE
R111 137 5 1.8
SN14 136 141 511 5 VON
EchN1 511 4 VALUE = { IF ( (V(184)<-3.2) & (V(184)<0), 6, 0.8 ) }
Ech11 141 4 VALUE = { IF ( (V(184)<-3.2) & (V(184)<0),((2.1-1.8)/(-3.2-(-5)))*(V(184)-(-5)), 0) }
SN12 136 146 512 4 VON
EchN2 512 4 VALUE = { IF ( (V(184)>=-3.2) & (V(184)<=3.8), 6 , 0.8 ) }
Ech12 146 4 VALUE = { IF ( (V(184)>=-3.2) & (V(184)<=3.8),((2.1-1.8)/((-3.2-(0.3))*(-3.2-(0.3))))*(V(184)-(0.3))*(V(184)-(0.3)), 0) }
SN13 136 144 513 4 VON
EchN3 513 4 VALUE = { IF ( (V(184)>3.8) & (V(184)>0), 6, 0.8 ) }
Ech13 144 4 VALUE = { IF ( (V(184)>3.8) & (V(184)>0),((1.8-2.1)/(5-3.8))*(V(184)-3.8)+0.3, 0) }
RIN1	136 4	1G
EOUT1 137 181	POLY(2) (136,4) (180,4) 0 0 0 0 0.999/1000
FCOPY1	4 180 VSENSE11 1
RSENSOR1 180 4	1K
VSENSE11 181 184	0
*
* TON/ TOFF/ BBM
S11 182 184 140 4 VON
S12 143 138 143 4 VEN
Ech14 143 4 VALUE = { IF(V(6)>=2.4, 5 , 0.8 ) }
eV1 140 4 138 4 1
C16 138 4 1e-012
*
* VOLTAGE SUPPLY REQUIREMENT
S13 2 182 185 4 VON
S14 142 185 142 4 VON
Ech15 142 4 VALUE = { IF((V(3)<=-0.5 & V(3)>=-5.5) & (V(1)<=5.5 & V(1)>=4.5), 5 , 0.01 ) }
S15 139 185 139 4 VON
Ech16 139 4 VALUE = { IF((V(3)>=-0.5 & (V(1)<=5.5 & V(1)>=0)), 5 , 0.01 ) }

.ENDS ADG601
