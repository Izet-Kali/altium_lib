* ADR5040 Spice Macro-model              
* Description: Shunt 2.048V voltage reference
* Generic Desk:ADR5040
* Developed by: ADI
* Revision History:
*   1.0 (10/2013)
* Copyright 2013 by Analog Devices, Inc.
*
* 
*              				cathode
*                			   | anode
*                			   | | 
*                			   | | 
*               			   | | 
.SUBCKT ADR5040__VOLTAGE_REFERENCE__1__2   1 8  


Q1 7 3 8 Qn1 
Q2 4 5 8 Qn2
Q3 8 6 1 Qp
R1 1 2 57.77k
R2 2 3 21.4k
R3 2 4 105k 
R4 3 5 2.715k
c1 4 6 10e-12
vinc 5 7 0
F1 1 8 vinc 0.7
G1 6 8 4 3 2e-4

.model Qn1 npn(bf=100 vaf=100)
.model Qn2 npn(bf=100 vaf=100)
.model Qp pnp(bf=20000 vaf=100)

.ends ADR5040
